// Match Data is from https://www.tennisabstract.com/charting/20170129-M-Australian_Open-F-Rafael_Nadal-Roger_Federer.html
//       *****
// 	    Player1: Right Handed, Roger Federer
// de_ct  mid  ad_ct
// -----+-----+-----
// |    |     |    |
// | 1  |  2  | 3  |
// |    |     |    |
// |===============|
// |    |     |    |
// | 4  |  5  | 6  |
// |    |     |    |
// -----+-----+-----
// ad_ct  mid  de_ct
// 	    Player2: Left Handed, Rafael Nadal
//       *****

enum{f_ad_ct, f_de_ct, n_ad_ct, n_de_ct}; //serve position: ad court or deuce court
enum{player1, player2, na};

var turn = na; //serve turn;
var fscore = 0;
var nscore = 0;
var won = na;
var ball = 9;

TieBreakGame = WhoServe1st; (Player1Serve [] Player2Serve);

WhoServe1st = []i:{f_de_ct,n_de_ct}@ TossCoin{turn = i} -> Skip;
Player1Serve = [won == na && turn == f_de_ct ]FedReady_DeCt -> De_Ply1Serve [] [won == na && turn == f_ad_ct] FedReady_AdCt -> Ad_Ply1Serve;

De_Ply1Serve = pcase {                             
			20: ServeT_in{ball= 5} -> Ply2_ForeHandR  
			28: ServeWide_in{ball =6} -> Ply2_BackHandR 
			0: ServeBody_in{ball=6} -> (Ply2_BackHandR [] Ply2_ForeHandR)
			24: Serve_err{ball=9} -> De_Ply1Serve_2nd}; 

De_Ply1Serve_2nd = pcase {                 //1st serve is out
			3: ServeT_in{ball= 5} -> Ply2_ForeHandR  
			13: ServeWide_in{ball =6} -> Ply2_BackHandR 
			5: ServeBody_in{ball=6} -> (Ply2_BackHandR [] Ply2_ForeHandR)
			3: Serve_err{ball=9} -> Fdoublefault{nscore++; if (nscore == 7) {won = player2} 
      		                                                        else { turn = (turn+1)%4} }-> NextPt
     		 };

Ad_Ply1Serve = pcase {
			21: ServeT_in{ball= 5} -> Ply2_BackHandR  
			16: ServeWide_in{ball =4} -> Ply2_ForeHandR
			0: ServeBody_in{ball=4} -> (Ply2_BackHandR [] Ply2_ForeHandR)
			29: Serve_err{ball=9} -> Ad_Ply1Serve_2nd}; 

Ad_Ply1Serve_2nd = pcase {                 //1st serve is out
			21: ServeT_in{ball= 5} -> Ply2_BackHandR  
			7: ServeWide_in{ball =4} -> Ply2_ForeHandR
			1: ServeBody_in{ball=4} -> (Ply2_BackHandR [] Ply2_ForeHandR)
			0: Serve_err{ball=9} -> Fdoublefault{nscore++; if (nscore == 7) {won = player2} 
      		                                                        else { turn = (turn+1)%4} }-> NextPt
     		 };    

NextPt = Player1Serve [] Player2Serve [] ([won != na] GameOver -> Skip);

// deuce stroke is when player2 hit position is 6
Ply2_de_stroke = pcase { // if already 
			43: BH_Crosscourt { ball = 1;} -> Ply1_de_stroke
			28: BH_Downline { ball = 3} -> Ply1_ad_stroke
			41: BH_DownMid { ball = 2} -> Ply1_mid_stroke
			7: FH_InsideIn {ball = 3} -> Ply1_ad_stroke
			4: FH_InsideOut { ball = 1} -> Ply1_de_stroke
			3: FH_DownMid { ball = 2} -> Ply1_mid_stroke
			34: Error { ball = 9} -> {fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt      		                                                            
	};

// mid stroke is when player2 hit position is 5
Ply2_mid_stroke = noMove -> pcase{
			20: FH_InsideOut{ball = 1} -> Ply1_de_stroke
			58: FH_Crosscourt { ball = 3} -> Ply1_ad_stroke
			20: FH_DownMid { ball = 2 } -> Ply1_mid_stroke
			3: BH_InsideOut { ball = 3} -> Ply1_ad_stroke
			17: BH_DownMid { ball = 2} -> Ply1_mid_stroke
			29: BH_Crosscourt { ball = 1} -> Ply1_de_stroke
			15: Error { ball = 9} -> {fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt   
	};
	

// ad stroke is when player 2 hit position is 4
Ply2_ad_stroke = pcase{
			60: FH_Crosscourt { ball = 3} -> Ply1_ad_stroke
			24: FH_Downline { ball = 1} -> Ply1_de_stroke
			10: FH_DownMid { ball = 2 } -> Ply1_mid_stroke
			0: BH_InsideOut { ball = 3} -> Ply1_ad_stroke
			0: BH_InsideIn { ball = 1} -> Ply1_de_stroke
			0: BH_DownMid { ball = 2} -> Ply1_mid_stroke
			53: Error { ball = 9} -> {fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt   	
	};    		                                                            


Player2Serve = [won == na && turn == n_de_ct]NadReady_DeCt -> De_Ply2Serve [] [ won == na && turn == n_ad_ct] NadReady_AdCt -> Ad_Ply2Serve;

De_Ply2Serve = pcase {
			24 : ServeT_in{ball= 2} -> Ply1_BackHandR
			7 : ServeWide_in{ball =1} -> Ply1_ForeHandR
			20: ServeBody_in{ball=1} -> (Ply1_ForeHandR [] Ply1_BackHandR)
			26: Serve_err{ball=9} -> De_Ply2Serve_2nd}; 

De_Ply2Serve_2nd = pcase {                 //1st serve is out
			11 : ServeT_in{ball= 2} -> Ply1_BackHandR
			7 : ServeWide_in{ball =1} -> Ply1_ForeHandR
			6: ServeBody_in{ball=1} -> (Ply1_ForeHandR [] Ply1_BackHandR)
			2 : Serve_err{ball=9} -> Ndoublefault{fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt
     		 };
     		 
Ad_Ply2Serve = pcase {
			9: ServeT_in{ball= 2} -> Ply1_ForeHandR
			32 : ServeWide_in{ball =3} -> Ply1_BackHandR
			18: ServeBody_in{ball=3} -> (Ply1_ForeHandR [] Ply1_BackHandR)
			15: Serve_err{ball=9} -> Ad_Ply2Serve_2nd}; 

Ad_Ply2Serve_2nd = pcase {                 //1st serve is out
			1: ServeT_in{ball= 2} -> Ply1_ForeHandR
			10 : ServeWide_in{ball =3} -> Ply1_BackHandR
			3: ServeBody_in{ball=3} -> (Ply1_ForeHandR [] Ply1_BackHandR)
			1: Serve_err{ball=9} -> Ndoublefault{fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt
     		 };
 	     
// deuce stroke is when player1 hit position is 1
Ply1_de_stroke = pcase{
			48: FH_Crosscourt { ball = 6} -> Ply2_de_stroke
			19: FH_Downline { ball = 4} -> Ply2_ad_stroke
			22: FH_DownMid { ball = 5} -> Ply2_mid_stroke
			0: BH_InsideIn {ball = 4} -> Ply2_ad_stroke
			0: BH_InsideOut { ball = 6} -> Ply2_de_stroke
			0: BH_DownMid { ball = 5} -> Ply2_mid_stroke
			48: Error { ball = 9} -> {nscore++; if (nscore == 7) {won = player2}
      		                                                     else { turn = (turn+1)%4} }-> NextPt      		                                                            
	};

// mid stroke is when player1 hit position is 2
Ply1_mid_stroke = pcase{
			23: FH_InsideOut{ball = 4} -> Ply2_ad_stroke
			43: FH_Crosscourt { ball = 6} -> Ply2_de_stroke
			17: FH_DownMid { ball = 5 } -> Ply2_mid_stroke
			11: BH_InsideOut { ball = 6} -> Ply2_de_stroke
			15: BH_DownMid { ball = 5} -> Ply2_mid_stroke
			8: BH_Crosscourt { ball = 4} -> Ply2_ad_stroke
			20: Error { ball = 9} -> {nscore++; if (nscore == 7) {won = player2}
      		                                                     else { turn = (turn+1)%4} }-> NextPt   
	};

// ad stroke is when player1 hit position is 3
Ply1_ad_stroke = pcase{
			60: BH_Crosscourt { ball = 4} -> Ply2_ad_stroke
			17: BH_Downline { ball = 6} -> Ply2_de_stroke
			47: BH_DownMid { ball = 5 } -> Ply2_mid_stroke
			16: FH_InsideOut { ball = 4} -> Ply2_ad_stroke
			8: FH_InsideIn { ball = 6} -> Ply2_de_stroke
			2: FH_DownMid { ball = 5} -> Ply2_mid_stroke
			33: Error { ball = 9} -> {nscore++; if (nscore == 7) {won = player2}
      		                                                     else { turn = (turn+1)%4} }-> NextPt 	
	};

Ply2_BackHandR = ifa (turn == f_de_ct) {    // Player2 backhand return to Player1 serve from the deuce court side
		  pcase {
			8: BH_CrossCourt_DE {ball = 1} -> Ply1_de_stroke
			3: BH_DownLine { ball = 3} -> Ply1_ad_stroke
			15: BH_DownMid_DE { ball = 2} -> Ply1_mid_stroke
			19: BH_Error_DE { ball = 9} -> {fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt
      		 }}
      		 else {
      		pcase {
			0: BH_InsideIn {ball = 1} -> Ply1_de_stroke
			3: BH_CrossCourt_AD {ball = 1} -> Ply1_de_stroke
			4: BH_InsideOut { ball = 3} -> Ply1_ad_stroke
			13: BH_DownMid_AD { ball = 2} -> Ply1_mid_stroke
			7: BH_Error_AD { ball = 9} -> {fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt
      		 }};
      		 
Ply2_ForeHandR = ifa (turn == f_de_ct) {    // Player2 forehand return to Player1 serve from the deuce court side
			pcase {
			3: FH_InsideOut { ball = 1} -> Ply1_de_stroke
			3: FH_CrossCourt_DE { ball = 3} -> Ply1_ad_stroke
			0: FH_InsideIn {ball = 3} -> Ply1_ad_stroke
			8: FH_DownMid_DE { ball = 2} -> Ply1_mid_stroke
			10: FH_Error_DE { ball = 9} -> {fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt
      		 }}
      		 else {
      		pcase {
			17: FH_CrossCourt_AD { ball = 3} -> Ply1_ad_stroke
			3: FH_DownLine { ball = 1} -> Ply1_de_stroke
			10: FH_DownMid_AD { ball = 2} -> Ply1_mid_stroke
			9: FH_Error_AD { ball = 9} -> {fscore++; if (fscore == 7) {won = player1} 
      		                                                     else { turn = (turn+1)%4} }-> NextPt			
      		 }};
      		 
Ply1_BackHandR = ifa (turn == n_de_ct) {    // Player2 backhand return to Player1 serve from the deuce court side
		 pcase {
			5: BH_CrossCourt_DE {ball = 4} -> Ply2_ad_stroke
			1: BH_InsideIn {ball = 4} -> Ply2_ad_stroke
			12: BH_InsideOut { ball = 6} -> Ply2_de_stroke
			18: BH_DownMid_DE { ball = 5} -> Ply2_mid_stroke
			4: BH_Error_DE { ball = 9} -> {nscore++; if (nscore == 7) {won = player2}
      		                                                     else { turn = (turn+1)%4} }-> NextPt
      		 }}
      		 else {
      		pcase {
			14: BH_CrossCourt_AD { ball = 4} -> Ply2_ad_stroke
			7: BH_DownLine { ball = 6} -> Ply2_de_stroke
			17: BH_DownMid_AD { ball = 5 } -> Ply2_mid_stroke
			14: BH_Error_AD{ball=9} -> {nscore++; if (nscore == 7) {won = player2}
      		                                                     else { turn = (turn+1)%4} }-> NextPt
      		 }};

Ply1_ForeHandR = ifa (turn == n_de_ct) {    // Player2 forehand return to Player1 serve from the deuce court side
		 pcase {
			11: FH_CrossCourt_DE { ball = 6;} -> Ply2_de_stroke
			1: FH_DownLine { ball = 4} -> Ply2_ad_stroke
			12: FH_DownMid_DE { ball = 5} -> Ply2_mid_stroke
			11: FH_Error_DE { ball = 9} -> {nscore++; if (nscore == 7) {won = player2}
      		                                                     else { turn = (turn+1)%4} }-> NextPt
      		 }}
      		 else {
      		pcase {
			0: FH_InsideOut { ball = 4} -> Ply2_ad_stroke
			2: FH_CrossCourt_AD {ball = 6} -> Ply2_de_stroke
			2: FH_InsideIn { ball = 6} -> Ply2_de_stroke
			12: FH_DownMid_AD { ball = 5} -> Ply2_mid_stroke
			5: FH_Error_AD { ball = 9} -> {nscore++; if (nscore == 7) {won = player2}
      		                                                     else { turn = (turn+1)%4} }-> NextPt
      		 }};


#define player1Win won == player1;
#assert TieBreakGame reaches player1Win with prob;
